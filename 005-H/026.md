Restless Gauze Swift

high

# GSPFunding.sol :: buyShares() A user may loss funds when sending different amounts of tokens to mint shares.

## Summary
**`buyShares()`** permits an user minting Shares first transferring tokens to the contract. However, a problem arises when users transfer unequal amounts of tokens (not transfering 50/50). This leads to potential user fund losses when sells their Shares as the Shares are minted based on the lowest mint rate.
## Vulnerability Detail
**`buyShares()`** serves to mint Shares for a user. In this process, the user transfers two tokens to the contract and subsequently calls the function. To better comprehend the issue at hand, let's delve into the calculation process for the minted Shares.

Retrieve the current balance of the contract, including the user's transferred amount:
```Solidity
uint256 baseBalance = _BASE_TOKEN_.balanceOf(address(this)) - _MT_FEE_BASE_;
uint256 quoteBalance = _QUOTE_TOKEN_.balanceOf(address(this)) - _MT_FEE_QUOTE_;
```
Access the previous balance of the contract stored in storage:
```Solidity
uint256 baseReserve = _BASE_RESERVE_;
uint256 quoteReserve = _QUOTE_RESERVE_;
```
Obtain the specific balance transferred by the user:
```Solidity
baseInput = baseBalance - baseReserve;
quoteInput = quoteBalance - quoteReserve;
```
If the **totalSupply** of Shares is zero, mint the shares based on the lower amount sent by the user. Otherwise, calculate a mint ratio to ensure the accurate minting of Shares for the user:
```Solidity
if (totalSupply == 0) {
            // case 1. initial supply
            // The shares will be minted to user
            shares = quoteBalance < DecimalMath.mulFloor(baseBalance, _I_)
                ? DecimalMath.divFloor(quoteBalance, _I_)
                : baseBalance;
            // The target will be updated
            _BASE_TARGET_ = uint112(shares);
            _QUOTE_TARGET_ = uint112(DecimalMath.mulFloor(shares, _I_));
        } else if (baseReserve > 0 && quoteReserve > 0) {
            // case 2. normal case
            uint256 baseInputRatio = DecimalMath.divFloor(baseInput, baseReserve);
            uint256 quoteInputRatio = DecimalMath.divFloor(quoteInput, quoteReserve);
            uint256 mintRatio = quoteInputRatio < baseInputRatio ? quoteInputRatio : baseInputRatio;
            // The shares will be minted to user
            shares = DecimalMath.mulFloor(totalSupply, mintRatio);
```
The problem arises when the user sends different amounts to the contract. In such cases, the minted ratio used is the lower of the two ratios. Consequently, this results in the user correctly minting shares for the lower transferred amount but incorrectly for the higher transferred amount.
```Solidity
uint256 mintRatio = quoteInputRatio < baseInputRatio ? quoteInputRatio : baseInputRatio;
```
To illustrate the issue more clearly, let's consider an example with numbers:

- Alice is the initial depositor and sends **2 DAI** and **2 USDC**, then calls **`buyShares()`**. She mints 2 Shares:
   - totalSupply = 2
   - baseReserve = 2
   - quoteReserve = 2
   
- Next, Bob deposits **2 DAI** and **4 USDC** and calls **`buyShares()`**. He mints 2 Shares. 

**`baseInputRatio = baseInput / baseReserve = 2 / 2 = 1`**
**`quoteInputRatio quoteInout / quoteReserve = 4 / 2 = 2 `**

Takes the lowest value to asign the mintRatio:

**`mintRatio = 1`**

**`shares = totalSupply * mintRatio = 2 * 1 = 2`**

As observed, Bob sends more **USDC** than Alice, but he only holds 50% of the total Shares. If Bob had sent **2 USDC** instead of **4 USDC**, he would have minted the same number of Shares.

If Alice sells her shares before Bob calling **`sellShares()`** to retrieve their tokens, Alice will receive more tokens, while Bob will receive fewer. This outcome stems from the calculation of the shares ratio in the following code:
```Solidity
baseAmount = baseBalance * shareAmount / totalShares;
quoteAmount = quoteBalance * shareAmount / totalShares;
_transferBaseOut(to, baseAmount);
_transferQuoteOut(to, quoteAmount);
```
Final balance of the users after selling the shares.

---------------Alice---------------
**`baseAmount = (4 * 2) / 4 = 2 DAI`**
**`quoteAmount  = (6 * 2) / 4 = 3 USDC`**

How you can see Alice initally only transfers **2 USDC** but obtains **3USDC**.  She obtains **1USDC** more.

---------------Bob---------------
**`baseAmount = (2 * 2) / 2 = 2 DAI`**
**`quoteAmount  = (3 * 2) / 2 = 3 USDC`**

How you can see Bob initally only transfers **4 USDC** but obtains **3 USDC**.  He lost **1 USDC**.

A malicious user can exploit this issue stoling tokens from other users, or even an ordinary user can acquire a greater quantity of tokens than originally sent.
## POC
To run it copy to the **`GSPFunding.t.sol`** file.
```Solidity
function test_buyAndSellShares_WithDifferentTokenAmounts() public {

        //First user buys Shares
        vm.startPrank(USER);

        uint256 userDaiBalanceBefore = dai.balanceOf(USER);
        uint256 userUsdcBalanceBefore = usdc.balanceOf(USER);
        uint256 otherDaiBalanceBefore = dai.balanceOf(OTHER);
        uint256 otherUsdcBalanceBefore = usdc.balanceOf(OTHER);
        
        dai.transfer(address(gsp), BASE_INPUT * 2);
        usdc.transfer(address(gsp), QUOTE_INPUT);

        gsp.buyShares(USER);
        
        console.log("FIRST USER SHARES:", gsp.balanceOf(address(USER)) / 1e18);
        uint256 userDaiBalanceMint = (dai.balanceOf(USER) / 1e18);
        uint256 userUsdcBalanceMint = (usdc.balanceOf(USER) / 1e6);

        vm.stopPrank();

        //Other users buys Shares
        vm.startPrank(OTHER);
        dai.transfer(address(gsp), BASE_INPUT * 2);
        usdc.transfer(address(gsp), QUOTE_INPUT * 2);

        gsp.buyShares(OTHER);
        console.log("SECOND USER SHARES:", gsp.balanceOf(address(OTHER)) / 1e18);
        uint256 otherDaiBalanceMint = (dai.balanceOf(OTHER) / 1e18);
        uint256 otherUsdcBalanceMint = (usdc.balanceOf(OTHER) / 1e6);

        vm.stopPrank();
        
        //Users sells shares
        vm.startPrank(USER);
        gsp.sellShares(BASE_INPUT * 2, USER, 0 , 0, "", block.timestamp);
        vm.stopPrank();

        vm.startPrank(OTHER);
        gsp.sellShares(BASE_INPUT * 2, OTHER, 0 , 0, "", block.timestamp);
        vm.stopPrank();

        uint256 userDaiBalanceAfter = dai.balanceOf(USER);
        uint256 userUsdcBalanceAfter = usdc.balanceOf(USER);
        uint256 otherDaiBalanceAfter = dai.balanceOf(OTHER);
        uint256 otherUsdcBalanceAfter = usdc.balanceOf(OTHER);

        console.log("---------Before Mint Sahres---------");
        console.log("FIRST USER DAI Balance Before:", (userDaiBalanceBefore) /  1e18);
        console.log("FIRST USER USDC Balance Before:", (userUsdcBalanceBefore) / 1e6) ;
        console.log("SECOND USER DAI Balance Before:", (otherDaiBalanceBefore) /  1e18);
        console.log("SECOND USER USDC Balance Before:", (otherUsdcBalanceBefore) / 1e6) ;

        console.log("---------Mint---------");
        console.log("FIRST USER DAI balance with Shares minted:", userDaiBalanceMint);
        console.log("FIRST USER USDC balance with Shares minted:", userUsdcBalanceMint);
        console.log("SECOND USER DAI balance with Shares minted:", otherDaiBalanceMint);
        console.log("SECOND USER USDC balance with Shares minted:", otherUsdcBalanceMint);

        console.log("---------After Share Sell---------");
        console.log("FIRST USER DAI Balance After:", (userDaiBalanceAfter) /  1e18);
        console.log("FIRST USER USDC Balance After:", (userUsdcBalanceAfter) / 1e6) ;
        console.log("SECOND USER DAI Balance After:", (otherDaiBalanceAfter) /  1e18);
        console.log("SECOND USER USDC Balance After:", (otherUsdcBalanceAfter) / 1e6) ;

        console.log("---------PROFIT AND LOSS---------");
        console.log("FIRST USER USDC PROFIT:", (userUsdcBalanceAfter - userUsdcBalanceBefore) / 1e6);
        console.log("SECOND USER USDC LOSS:", (otherUsdcBalanceBefore - otherUsdcBalanceAfter) / 1e6);   
    }
```
---------------------------LOGS---------------------------
```Solidity
FIRST USER SHARES: 2000
SECOND USER SHARES: 2000

 ---------Before Mint Sahres---------
 FIRST USER DAI Balance Before: 11000
 FIRST USER USDC Balance Before: 12000
 SECOND USER DAI Balance Before: 11000
 SECOND USER USDC Balance Before: 12000
 ---------Mint---------
 FIRST USER DAI balance with Shares minted: 9000
 FIRST USER USDC balance with Shares minted: 10000
 SECOND USER DAI balance with Shares minted: 9000
 SECOND USER USDC balance with Shares minted: 8000
 ---------After Share Sell---------
 FIRST USER DAI Balance After: 11000
 FIRST USER USDC Balance After: 13000
 SECOND USER DAI Balance After: 11000
 SECOND USER USDC Balance After: 11000
  ---------PROFIT AND LOSS---------
 FIRST USER USDC PROFIT: 1000
 SECOND USER USDC LOSS: 1000
```
To replicate the values visible in the logs, make the following adjustments in the test:
```Solidity
uint256 constant BASE_RESERVE = 1e19 * 1000; // 10000 DAI
uint256 constant QUOTE_RESERVE = 1e7 * 1000; // 10000 USDC
uint256 constant BASE_INPUT = 1e18 * 1000; // 1000 DAI
uint256 constant QUOTE_INPUT = 2e6 * 1000; // 2000 USDC
```
## Impact
A malicious user can exploit this situation to steal user funds, or ordinary users can acquire a larger quantity of tokens at the expense of others.
## Code Snippet
https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/af43d39f6a89e5084843e196fc0185abffe6304d/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSPFunding.sol#L31-L82
https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/af43d39f6a89e5084843e196fc0185abffe6304d/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSPFunding.sol#L92-L143
## Tool used
Manual Review and Foundry.

## Recommendation
It is challenging to propose a definitive solution for this issue as the desired implementation by the developers is unclear. 

However, one potential solution could be to require users to transfer an equal amount of both **token1** and **token2** to address this problem. 

Another solution could involve implementing separate Shares, such as **shares1** and **shares2**, to accurately mint the corresponding Shares for each token.