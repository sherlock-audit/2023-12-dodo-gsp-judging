Huge Magenta Tardigrade

medium

# `GSPVault.sol#adjustPrice(uint256)` can increase the oracle price `_I_` in excess of the intended maxima.

## Summary

It is intended that calls to [`adjustPrice(uint256)`](https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/af43d39f6a89e5084843e196fc0185abffe6304d/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSPVault.sol#L169) are protected by the [`_PRICE_LIMIT_`](https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/af43d39f6a89e5084843e196fc0185abffe6304d/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSPStorage.sol#L75) to prevent excessive changes in guide prices for the stablecoin oracle [`_I_`](https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/af43d39f6a89e5084843e196fc0185abffe6304d/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSPStorage.sol#L73).

However, `adjustPrice(uint256)` can be called multiple times either in succession; or more realistically, over the course of multiple transactions.

This can inadvertently move the price far in excess of the restrictions originally intended by the pool-defined `_PRICE_LIMIT_`. Furthermore, it can be demonstrated that movements in price can even surpass the protocol's maximum possible intended [threshold invariant](https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/af43d39f6a89e5084843e196fc0185abffe6304d/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSPVault.sol#L161).

> [!NOTE]  
> The intention of the price limit is to safeguard against excessive price swings that could be caused by an honest maintainer, and to ensure pool configurations cannot undermine protocol-level invariants.
>
> Importantly, this issue **does not** rely on the maintainer to abuse their trusted position in the protocol - it instead highlights a failing of the intended restrictions on the capabilities of the maintainer role.

## Vulnerability Detail

When a maintainer makes calls to `adjustPriceLimit(uint256 priceLimit)`, the protocol enforces that they cannot move the price past the pool-defined `_PRICE_LIMIT_`, which itself is bounded by a [maximum upper limit](https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/af43d39f6a89e5084843e196fc0185abffe6304d/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSPVault.sol#L161) of `1e6`:

```solidity
/**
 * @notice PriceLimit is used for oracle change protection
 * @notice It sets a ratio where the relative deviation between the new price and the old price cannot exceed this ratio.
 * @dev The default priceLimit is 1e3, the decimals of priceLimit is 1e6
 * @param priceLimit The new price limit
 */
function adjustPriceLimit(uint256 priceLimit) external onlyMaintainer {
    // the default priceLimit is 1e3
    require(priceLimit <= 1e6, "INVALID_PRICE_LIMIT");
    _PRICE_LIMIT_ = priceLimit;
}

/**
 * @notice Adjust oracle price i, only for maintainer
 * @param i The new oracle price
 */
function adjustPrice(uint256 i) external onlyMaintainer {
    // the difference between i and _I_ should be less than priceLimit
    uint256 offset = i > _I_ ? i - _I_ : _I_ - i;
    require((offset * 1e6 / _I_) <= _PRICE_LIMIT_, "EXCEED_PRICE_LIMIT");
    _I_ = i;
}
```

However, between calls to `adjustPriceLimit(uint256 priceLimit)`, `_I_` is changed, resulting in a new maximum price limit:

```solidity
function test_sherlock_ignorePriceLimit() public {
    GSP gsp = new DeployGSP().run();

    // Here we'll set aside two important metrics - the maximum
    // intended price enforced by `PRICE_LIMIT`, and the initial
    // price of the deployed `GSP`.
    uint256 INTENDED_MAXIMUM_PRICE = 1_001_000;
    uint256 INITIAL_PRICE = 1_000_000;

    assertEq(gsp._I_(), INITIAL_PRICE);
    assertEq(gsp._PRICE_LIMIT_(), 1_000);

    // Using the current configuration, the maximum
    // limit on a manual increase in the oracle price
    // must be 1_001_000, therefore an attempt to
    // set the price to 1_001_001 should fail:
    vm.prank(MAINTAINER);
    vm.expectRevert("EXCEED_PRICE_LIMIT");
    gsp.adjustPrice(INTENDED_MAXIMUM_PRICE + 1);

    // Next, let's take the pool to the maximum
    // price as allowed by the `_PRICE_LIMIT_`:
    vm.prank(MAINTAINER);
    gsp.adjustPrice(INTENDED_MAXIMUM_PRICE);

    assertEq(gsp._I_(), INTENDED_MAXIMUM_PRICE);

    // Now we have increased the price, the meaning
    // of `PRICE_LIMIT` now relies upon the inflated
    // price when computing new limits, and can
    // therefore be bypassed.

    // The new maximum possible price is `1_002_001`:
    vm.prank(MAINTAINER);
    gsp.adjustPrice(1_002_001);

    assertEq(gsp._I_(), 1_002_001);

    // This can continue arbitrarily, entirely circumventing
    // the intended protections for the stablecoin price oracle.
    // For demonstration purposes, let's use three iterations.
    for (uint256 j = 0; j < 3;) {
        // Move the price oracle to the next maximum limit:
        uint256 i = ((gsp._PRICE_LIMIT_() * gsp._I_()) / 1_000_000) + gsp._I_();

        vm.prank(MAINTAINER);
        gsp.adjustPrice(i);

        unchecked { ++j; }
    }

    // Here, we mark the current price indicated by the price oracle after
    // performing three executions about the price limit.
    uint256 EXCESSIVE_CURRENT_PRICE = 1_005_010;
    assertEq(gsp._I_(), EXCESSIVE_CURRENT_PRICE);

    // Finally, let's compute the evaluated price increase
    // relative to the original price the oracle started at:
    uint256 priceIncreasePercent = ((((EXCESSIVE_CURRENT_PRICE * 10 ** 18) / INITIAL_PRICE)) - 10 ** 18) * 100;

    // The relative price increase compared to the
    // original price is now 0.5% - the original intended constraint
    // was 0.1%.
    assertEq(priceIncreasePercent, 501000000000000000); // 0.501
}
```

```shell
Running 1 test for test/GSPVault.Sherlock.t.sol:TestGSPVaultSherlock
[PASS] test_sherlock_ignorePriceLimit() (gas: 7906805)
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 2.20s
 
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

It can be surmised that this can continue indefinitely.

## Impact

Price oracle `_I_` can be manually moved far in excess of intended boundaries, leading to:

- Reduced safeguards for liquidity providers
- Increased risk of impermanent loss
- An unintended increase in the influence of the maintainer role over the underlying pool

## Code Snippet

```solidity
/**
 * @notice PriceLimit is used for oracle change protection
 * @notice It sets a ratio where the relative deviation between the new price and the old price cannot exceed this ratio.
 * @dev The default priceLimit is 1e3, the decimals of priceLimit is 1e6
 * @param priceLimit The new price limit
 */
function adjustPriceLimit(uint256 priceLimit) external onlyMaintainer {
    // the default priceLimit is 1e3
    require(priceLimit <= 1e6, "INVALID_PRICE_LIMIT");
    _PRICE_LIMIT_ = priceLimit;
}

/**
 * @notice Adjust oracle price i, only for maintainer
 * @param i The new oracle price
 */
function adjustPrice(uint256 i) external onlyMaintainer {
    // the difference between i and _I_ should be less than priceLimit
    uint256 offset = i > _I_ ? i - _I_ : _I_ - i;
    require((offset * 1e6 / _I_) <= _PRICE_LIMIT_, "EXCEED_PRICE_LIMIT");
    _I_ = i;
}
```

## Tool used

Visual Studio Code

## Recommendation

The value of the price oracle `_I_` on pool creation should be a cached immutable, and this should be the value compared against when trying to implement manual price adjustment.

### [GSPVault.sol](https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/main/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSPVault.sol)

```diff
/**
 * @notice Adjust oricle price i, only for maintainer
 * @param i The new oracle price
 */
function adjustPrice(uint256 i) external onlyMaintainer {
-    // the difference between i and _I_ should be less than priceLimit
+    // the difference between i and _I0_ should be less than priceLimit
-    uint256 offset = i > _I_ ? i - _I_ : _I_ - i;
+    uint256 offset = i > _I0_ ? i - _I0_ : _I0_ - i;
-    require((offset * 1e6 / _I_) <= _PRICE_LIMIT_, "EXCEED_PRICE_LIMIT");
+    require((offset * 1e6 / _I0_) <= _PRICE_LIMIT_, "EXCEED_PRICE_LIMIT");
    _I_ = i;
}
```

### [GSPStorage.sol](https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/main/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSPStorage.sol)

```diff
    uint256 public _K_;
+   uint256 public _I0_;
    uint256 public _I_;
    // _PRICE_LIMIT_ is 1/1000 by default, which is used to limit the setting range of I
    uint256 public _PRICE_LIMIT_ = 1e3;
```

### [GSP.sol](https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/main/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSP.sol)

```diff
        // i should be greater than 0 and less than 10**36
        require(i > 0 && i <= 10**36);
+       _I0_ = i;
        _I_ = i;
        // k should be greater than 0 and less than 10**18
        require(k <= 10**18);
```
