Huge Magenta Tardigrade

medium

# `GSP.sol` can be initialized with values of `_MT_FEE_RATE_` in excess of the intended maxima.

## Summary

Lack of validation on the [`mtFeeRate`](https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/af43d39f6a89e5084843e196fc0185abffe6304d/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSPStorage.sol#L69) parameter in [`init(address,address,address,uint256,uint256,uint256,uint256,bool)`](https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/af43d39f6a89e5084843e196fc0185abffe6304d/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSP.sol#L34C19-L34C19) can lead to the deployment of a [`GSP`](https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/main/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSP.sol) that can be instantiated with values of [`MT_FEE_RATE`](https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/af43d39f6a89e5084843e196fc0185abffe6304d/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSPStorage.sol#L69) that disrespects protocol invariants.

Similarly, the lack of validation checks for over-unity summation of [`MT_FEE_RATE`](https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/af43d39f6a89e5084843e196fc0185abffe6304d/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSPStorage.sol#L69) and [`LP_FEE_RATE`](https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/af43d39f6a89e5084843e196fc0185abffe6304d/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSPStorage.sol#L71) can induce denial of service for token swaps.

## Vulnerability Detail

When a [`_MAINTAINER_`](https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/af43d39f6a89e5084843e196fc0185abffe6304d/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSPStorage.sol#L28) attempts to update [`MT_FEE_RATE`](https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/af43d39f6a89e5084843e196fc0185abffe6304d/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSPStorage.sol#L69) via a call to [`adjustMtFeeRate(uint256)`](https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/af43d39f6a89e5084843e196fc0185abffe6304d/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSPVault.sol#L181), they are restricted from providing values over unity (`10 ** 18`):

```solidity
/**
 * @notice Adjust mtFee rate, only for maintainer
 * @dev The decimals of mtFee rate is 1e18
 * @param mtFeeRate The new mtFee rate
 */
function adjustMtFeeRate(uint256 mtFeeRate) external onlyMaintainer {
    require(mtFeeRate <= 10**18, "INVALID_MT_FEE_RATE");
    _MT_FEE_RATE_ = mtFeeRate;
}
```

However, no such validation is performed when initially constructing a pool:

```solidity
function test_sherlock_overUnity() public {
    MockERC20 b = new MockERC20("Base", "B", 18);
    MockERC20 q = new MockERC20("Quote", "Q", 18);

    address MAINTAINER = 0x95C4F5b83aA70810D4f142d58e5F7242Bd891CB0;
    uint256 LP_FEE_RATE = 0;
    uint256 MT_FEE_RATE = (10 ** 18) + (10 ** 18); // @audit 1.01%
    uint256 I = 1000000;
    uint256 K = 500000000000000;
    bool IS_OPEN_TWAP = false;

    GSP gsp = new GSP();
    gsp.init(MAINTAINER, address(b), address(q), LP_FEE_RATE, MT_FEE_RATE, I, K, IS_OPEN_TWAP);

    address alice = address(0x69);

    b.mint(alice, 10 ** 18);
    q.mint(alice, 10 ** 18);

    vm.startPrank(alice);
    b.transfer(address(gsp), 10 ** 18);
    q.transfer(address(gsp), 10 ** 18);
    (uint256 aliceSharesMinted,,) = gsp.buyShares(alice);
    assertEq(aliceSharesMinted, 1000000000000000000);
    vm.stopPrank();

    address bob = address(0x420);
    b.mint(bob, 5 ** 17);

    vm.startPrank(bob);
    b.transfer(address(gsp), 5 ** 17);

    vm.expectRevert(); // @audit Bob cannot trade.
    gsp.sellBase(bob);

    vm.stopPrank();

    vm.startPrank(alice);
    gsp.sellShares(aliceSharesMinted, alice, 0, 0, "", block.timestamp);
    vm.stopPrank();
}
```

```shell
Running 1 test for test/GPSTrader.Sherlock.t.sol:TestGSPTraderSherlock
[PASS] test_sherlock_overUnity() (gas: 5216344)
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 2.05s
 
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Impact

1. Loss of deployment gas costs for the [`_MAINTAINER_`](https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/af43d39f6a89e5084843e196fc0185abffe6304d/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSPStorage.sol#L28).
2. Denial of service for token swaps.
3. Opportunity cost for LPs, who may successfully provide liquidity to pools, but will not accrue any fees until the position is corrected (due to numeric overflow when calculating fees from swaps).

## Code Snippet

```solidity
/**
 * @notice Function will be called in factory, init risk should not be included.
 * @param maintainer The dodo's address, who can claim mtFee and own this pool
 * @param baseTokenAddress The base token address
 * @param quoteTokenAddress The quote token address
 * @param lpFeeRate The rate of lp fee, with 18 decimal
 * @param mtFeeRate The rate of mt fee, with 18 decimal
 * @param i The oracle price, possible to be changed only by maintainer
 * @param k The swap curve parameter
 * @param isOpenTWAP Use TWAP price or not
 */
function init(
    address maintainer,
    address baseTokenAddress,
    address quoteTokenAddress,
    uint256 lpFeeRate,
    uint256 mtFeeRate,
    uint256 i,
    uint256 k,
    bool isOpenTWAP
) external {
    // GSP can only be initialized once
    require(!_GSP_INITIALIZED_, "GSP_INITIALIZED");
    // _GSP_INITIALIZED_ is set to true after initialization
    _GSP_INITIALIZED_ = true;
    // baseTokenAddress and quoteTokenAddress should not be the same
    require(baseTokenAddress != quoteTokenAddress, "BASE_QUOTE_CAN_NOT_BE_SAME");
    // _BASE_TOKEN_ and _QUOTE_TOKEN_ should be valid ERC20 tokens
    _BASE_TOKEN_ = IERC20(baseTokenAddress);
    _QUOTE_TOKEN_ = IERC20(quoteTokenAddress);

    // i should be greater than 0 and less than 10**36
    require(i > 0 && i <= 10**36);
    _I_ = i;
    // k should be greater than 0 and less than 10**18
    require(k <= 10**18);
    _K_ = k;

    // _LP_FEE_RATE_ is set when initialization
    _LP_FEE_RATE_ = lpFeeRate;
    // _MT_FEE_RATE_ is set when initialization
    _MT_FEE_RATE_ = mtFeeRate;
    // _MAINTAINER_ is set when initialization, the address receives the fee
    _MAINTAINER_ = maintainer;
    _IS_OPEN_TWAP_ = isOpenTWAP;
    // if _IS_OPEN_TWAP_ is true, _BLOCK_TIMESTAMP_LAST_ is set to the current block timestamp
    if (isOpenTWAP) _BLOCK_TIMESTAMP_LAST_ = uint32(block.timestamp % 2**32);

    string memory connect = "_";
    string memory suffix = "GSP";
    // name of the shares is the combination of suffix, connect and string of the GSP
    name = string(abi.encodePacked(suffix, connect, addressToShortString(address(this))));
    // symbol of the shares is GLP
    symbol = "GLP";
    // decimals of the shares is the same as the base token decimals
    decimals = IERC20Metadata(baseTokenAddress).decimals();

    // ============================== Permit ====================================
    uint256 chainId;
    assembly {
        chainId := chainid()
    }
    // DOMAIN_SEPARATOR is used for approve by signature
    DOMAIN_SEPARATOR = keccak256(
        abi.encode(
            // keccak256('EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)'),
            0x8b73c3c69bb8fe3d512ecc4cf759cc79239f7b179b0ffacaa9a75d522b39400f,
            keccak256(bytes(name)),
            keccak256(bytes("1")),
            chainId,
            address(this)
        )
    );
    // ==========================================================================
}
```

## Tool used

Visual Studio Code

## Recommendation

When initializing a pool, ensure that the deployer cannot specify a value of [`mtFeeRate`](https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/af43d39f6a89e5084843e196fc0185abffe6304d/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSPStorage.sol#L69) greater than `10**18`:

### [GSP.sol](https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/main/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSP.sol)

```diff
// _LP_FEE_RATE_ is set when initialization
_LP_FEE_RATE_ = lpFeeRate;
+ // mtFeeRate should be less than or equal to 10**18
+  require(mtFeeRate <= 10**18);
// _MT_FEE_RATE_ is set when initialization
_MT_FEE_RATE_ = mtFeeRate;
```