Lone Boysenberry Peacock

medium

# Flashloans do not deduct fees, leaking value from protocol

## Summary
Users are able to take fee-free flashloans from the protocol. While it might be an intentional design choice, it is pretty common to charge users for utilizing the protocol's capital in flash loans, in the same time receiving a small fee off it, which is a value add for the protocol. Here it was noticed, that users do not have to pay for flashloans at all.

## Vulnerability Detail
In function [flashLoan](https://github.com/sherlock-audit/2023-12-dodo-gsp/blob/main/dodo-gassaving-pool/contracts/GasSavingPool/impl/GSPTrader.sol#L122), the protocol accepts that user returns from flash loan at least exact balance that was there before the flashLoan, or if there is imbalance between base/quote token, then the imbalance should be enough to swap the difference to the other token plus the swap fees (`mtFee`) which are returned by `querySell*`. However as long as this is met, no additional fee is collected off the users.

## Impact
Protocol is missing fees from flash loans. Offering flash loans is an extra utility, which is normally charged a small fee off it. Here, the protocol offers it completely free of charge, so is missing income from the additional fees.

## Code Snippet

Below snippet shows the function code without any fee collection implemented.

```solidity
    function flashLoan(
        uint256 baseAmount,
        uint256 quoteAmount,
        address assetTo,
        bytes calldata data
    ) external nonReentrant {
        _transferBaseOut(assetTo, baseAmount);
        _transferQuoteOut(assetTo, quoteAmount);

        if (data.length > 0)
            IDODOCallee(assetTo).DSPFlashLoanCall(msg.sender, baseAmount, quoteAmount, data);

        uint256 baseBalance = _BASE_TOKEN_.balanceOf(address(this)) - _MT_FEE_BASE_;
        uint256 quoteBalance = _QUOTE_TOKEN_.balanceOf(address(this)) - _MT_FEE_QUOTE_;

        // no input -> pure loss
        require(
            baseBalance >= _BASE_RESERVE_ || quoteBalance >= _QUOTE_RESERVE_,
            "FLASH_LOAN_FAILED"
        );

        // sell quote case
        // quote input + base output
        if (baseBalance < _BASE_RESERVE_) {
            uint256 quoteInput = quoteBalance - uint256(_QUOTE_RESERVE_);
            (
                uint256 receiveBaseAmount,
                uint256 mtFee,
                PMMPricing.RState newRState,
                uint256 newQuoteTarget
            ) = querySellQuote(tx.origin, quoteInput); // revert if quoteBalance<quoteReserve
            require(
                (uint256(_BASE_RESERVE_) - baseBalance) <= receiveBaseAmount,
                "FLASH_LOAN_FAILED"
            );
            
            _MT_FEE_BASE_ = _MT_FEE_BASE_ + mtFee;
            
            if (_RState_ != uint32(newRState)) {
                require(newQuoteTarget <= type(uint112).max, "OVERFLOW");
                _QUOTE_TARGET_ = uint112(newQuoteTarget);
                _RState_ = uint32(newRState);
                emit RChange(newRState);
            }
            emit DODOSwap(
                address(_QUOTE_TOKEN_),
                address(_BASE_TOKEN_),
                quoteInput,
                receiveBaseAmount,
                msg.sender,
                assetTo
            );
        }

        // sell base case
        // base input + quote output
        if (quoteBalance < _QUOTE_RESERVE_) {
            uint256 baseInput = baseBalance - uint256(_BASE_RESERVE_);
            (
                uint256 receiveQuoteAmount,
                uint256 mtFee,
                PMMPricing.RState newRState,
                uint256 newBaseTarget
            ) = querySellBase(tx.origin, baseInput); // revert if baseBalance<baseReserve
            require(
                (uint256(_QUOTE_RESERVE_) - quoteBalance) <= receiveQuoteAmount,
                "FLASH_LOAN_FAILED"
            );

            _MT_FEE_QUOTE_ = _MT_FEE_QUOTE_ + mtFee;
            
            if (_RState_ != uint32(newRState)) {
                require(newBaseTarget <= type(uint112).max, "OVERFLOW");
                _BASE_TARGET_ = uint112(newBaseTarget);
                _RState_ = uint32(newRState);
                emit RChange(newRState);
            }
            emit DODOSwap(
                address(_BASE_TOKEN_),
                address(_QUOTE_TOKEN_),
                baseInput,
                receiveQuoteAmount,
                msg.sender,
                assetTo
            );
        }

        _sync();

        emit DODOFlashLoan(msg.sender, assetTo, baseAmount, quoteAmount);
    }
```


## Tool used

Manual Review

## Recommendation
Deduct a small fee off the flashloan, e.g. calculate a base/quote fee off the borrowed amounts and on return from flashloan check if 
```solidity
//[calculate fl_*_fee based on _MT_FEE_RATE or anything you see fit] in the beginning of function
//line 138 can be then
require(
            baseBalance >= _BASE_RESERVE_ + fl_base_fee || quoteBalance >= _QUOTE_RESERVE_ + fl_quote_fee,
            "FLASH_LOAN_FAILED"
        );

_MT_FEE_QUOTE_ = _MT_FEE_QUOTE_ + fl_quote_fee;
_MT_FEE_BASE_ = _MT_FEE_BASE_ + fl_base_fee ;
```